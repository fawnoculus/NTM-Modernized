plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

sourceSets {
    main {
        resources.srcDirs(
            "../common/src/main/generated",
            "../common/src/main/resources"
        )
    }
}

loom {
    runs {
        client {
            property "fabric.log.level", "debug"
        }

        server {
            property "fabric.log.level", "debug"
        }
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"

    modImplementation "dev.architectury:architectury-neoforge:${architectury_api_version}"

    /*
    // Compile Time Stuff
    modCompileOnly "maven.modrinth:jade:${neoforge_jade_version}"                                  // Jade
    modCompileOnly "mezz.jei:jei-${neoforge_jei_mc_version}-neoforge-api:${neoforge_jei_version}"      // JEI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-neoforge:${neoforge_rei_version}"            // REI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-neoforge:${neoforge_rei_version}" // REI default Plugins
    modCompileOnly "dev.emi:emi-neoforge:${neoforge_emi_version}+${neoforge_emi_mc_version}:api"       // EMI
     */

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.WARN)

    inputs.property "mod_version", mod_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "neoforge_version", neoforge_version
    inputs.property "architectury_version", architectury_api_version
    filteringCharset "UTF-8"

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version,
            "minecraft_version": inputs.properties.minecraft_version,
            "neoforge_version": inputs.properties.neoforge_version,
            "architectury_version": inputs.properties.architectury_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
