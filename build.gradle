plugins {
    id 'dev.architectury.loom' version "${architectury_loom_version}" apply false
    id 'architectury-plugin' version "${architectury_plugin_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_gradle_version}" apply false
}

architectury {
    minecraft = minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_name
    version = mod_version
    group = maven_group
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-1.21.11-fabric`.
        archivesName = "${archives_name}-${minecraft_version}-${project.name}"
    }

    repositories {
        // Parchment
        exclusiveContent {
            forRepository {
                repositories.maven {
                    name = 'ParchmentMC'
                    url = 'https://maven.parchmentmc.org'
                }
            } filter {
                includeGroup "org.parchmentmc.data"
            }
        }

        // REI
        exclusiveContent {
            forRepository {
                repositories.maven {
                    name = "Shedaniel's Maven"
                    url = "https://maven.shedaniel.me/"
                }
            } filter {
                includeGroup "me.shedaniel"
                includeGroup "me.shedaniel.cloth"
            }
        }

        // JEI
        exclusiveContent {
            forRepository {
                // JEI
                repositories.maven {
                    name = "Jared's maven"
                    url = "https://maven.blamejared.com/"
                }
            } filter {
                includeGroup "mezz.jei"
            }
        }

        // EMI & Modmenu
        exclusiveContent {
            forRepository {
                repositories.maven {
                    name = "Terraformers"
                    url = "https://maven.terraformersmc.com/"
                }
            } filter {
                includeGroup "dev.emi"
                includeGroup "com.terraformersmc"
            }
        }

        // Jade & Sodium
        exclusiveContent {
            forRepository {
                repositories.maven {
                    name = "Modrinth Maven"
                    url = "https://api.modrinth.com/maven"
                }
            } filter {
                includeGroup "maven.modrinth"
            }
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    jar {
        from rootProject.file("LICENSE.txt")
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}

