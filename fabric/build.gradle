plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

sourceSets {
    main {
        resources.srcDirs(
            "../common/src/main/generated",
            "../common/src/main/resources"
        )
    }
}

loom {
    runs {
        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../common/src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=ntm"
            runDir "build/datagen"
        }
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    modImplementation "dev.architectury:architectury-fabric:${architectury_api_version}"

    // Libraries required by REI
    // modApi "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"

    // Compatibility Tests & better Dev Environment
    modLocalRuntime "com.terraformersmc:modmenu:${fabric_modmenu_version}"                 // ModMenu
    //modLocalRuntime "maven.modrinth:jade:${fabric_jade_version}"                         // Jade (Uses Loom 1.14 but architectury currently only supports 1.13)
    modLocalRuntime "maven.modrinth:sodium:${fabric_sodium_version}"                       // Sodium
    //modLocalRuntime "mezz.jei:jei-${fabric_jei_mc_version}-fabric:${fabric_jei_version}" // JEI (Uses Loom 1.14 but architectury currently only supports 1.13)
    //modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"              // REI (there is no version for 1.21.11 yet)
    //modLocalRuntime "dev.emi:emi-fabric:${emi_version}+${emi_mc_version}"                // EMI (there is no version for 1.21.11 yet)

    // Compile Time Stuff
    modCompileOnly "com.terraformersmc:modmenu:${fabric_modmenu_version}"                        // ModMenu
    modCompileOnly "maven.modrinth:jade:${fabric_jade_version}"                                  // Jade
    modCompileOnly "mezz.jei:jei-${fabric_jei_mc_version}-fabric-api:${fabric_jei_version}"      // JEI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${fabric_rei_version}"            // REI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${fabric_rei_version}" // REI default Plugins
    modCompileOnly "dev.emi:emi-fabric:${fabric_emi_version}+${fabric_emi_mc_version}:api"       // EMI

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.WARN)

    inputs.property "mod_version", mod_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", fabric_loader_version
    inputs.property "fabric_api_version", fabric_api_version
    inputs.property "architectury_version", architectury_api_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.mod_version,
            "minecraft_version": inputs.properties.minecraft_version,
            "loader_version": inputs.properties.loader_version,
            "fabric_api_version": inputs.properties.fabric_api_version,
            "architectury_version": inputs.properties.architectury_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
